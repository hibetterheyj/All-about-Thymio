<!DOCTYPE aesl-source>
<network>


<!--list of global events-->


<!--list of constants-->
<constant value="500" name="MOTOR"/>
<constant value="4000" name="TARGET"/>
<constant value="100" name="TARGET_ERROR"/>


<!--show keywords state-->
<keywords flag="false"/>


<!--node thymio-II-->
<node nodeId="62941" name="thymio-II"># Thymio-II program: on-off controller for approaching
# Copyright 2013, 2017 by Moti Ben-Ari
# CreativeCommons BY-SA 3.0

# Constants
#  TARGET        Highest value of the proximity sensor
#  TARGET_ERROR  Stop when error is below this value
#  MOTOR         Power setting for the motor

# Center button to start and stop
# Robot approaches object detected by center sensor

var state  # The state 0 = off, 1 = on
var error  # Difference between sensor and target values

state = 0
motor.left.target  = 0
motor.right.target = 0
call leds.circle(0,0,0,0,0,0,0,0)

# Stop the algorithm and the motors
sub stop
  state = 0
  motor.left.target  = 0
  motor.right.target = 0
  call leds.circle(0,0,0,0,0,0,0,0)

# Proximity event handler
onevent prox
  # Do nothing in state 0
  if state == 0 then return end
  # Compute the error
  error = abs (TARGET - prox.horizontal[2])
  # If it is less than the target error, stop
  if error &lt; TARGET_ERROR then
    callsub stop
  #   otherwise, set motors to full power
  else
    call leds.circle(32,0,0,0,0,0,0,0)
    motor.left.target  =  MOTOR
    motor.right.target =  MOTOR
  end

# Center button event handler
onevent button.center
  if button.center == 1 then return end
  if state == 0 then
    state = 1
  else
    callsub stop
  end
</node>


</network>
